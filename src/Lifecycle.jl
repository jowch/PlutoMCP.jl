# D15 — deferred Pluto session lifecycle (standalone connect + lifecycle MCP tools).

const LIFECYCLE_TOOLS = Set([
    "pluto_session_status",
    "start_pluto_session",
    "stop_pluto_session",
    "open_notebook",
    "new_notebook",
    "allow_execution",
])

is_lifecycle_tool(name::AbstractString) = name in LIFECYCLE_TOOLS

const _STANDALONE_SESSION = Ref{Any}(nothing)
const _STANDALONE_HTTP_TASK = Ref{Union{Nothing,Task}}(nothing)
const _STANDALONE_HTTP_SERVER = Ref{Any}(nothing)
const _STANDALONE_PLUTO_SERVER = Ref{Any}(nothing)
const _STANDALONE_PLUTO_TASK = Ref{Union{Nothing,Task}}(nothing)
const _STANDALONE_PLUTO_PORT = Ref{Union{Nothing,Int}}(1234)
const _STANDALONE_MCP_PORT = Ref{Union{Nothing,Int}}(2346)
const _STANDALONE_PLUTO_PORT_HINT = Ref(1234)
const _STANDALONE_MCP_PORT_HINT = Ref(2346)
const _STANDALONE_REQUIRE_SECRET = Ref(true)
const _CONTROL_BRIDGE_OWNED = Ref(false)
const _HTTP_BRIDGE_RUNNER = Ref{Function}(
    (session, port; kwargs...) -> error("HTTP bridge not registered"),
)

function register_http_bridge!(f::Function)
    _HTTP_BRIDGE_RUNNER[] = f
end

function configure_standalone!(;
    pluto_port=1234,
    mcp_port=2346,
    pluto_port_hint=nothing,
    mcp_port_hint=nothing,
    require_secret_for_access=true,
)
    hint_pluto = pluto_port_hint === nothing ? Int(pluto_port) : Int(pluto_port_hint)
    hint_mcp = mcp_port_hint === nothing ? Int(mcp_port) : Int(mcp_port_hint)
    _STANDALONE_PLUTO_PORT_HINT[] = hint_pluto
    _STANDALONE_MCP_PORT_HINT[] = hint_mcp
    # Legacy callers still treat these as fixed ports until listenany overrides them.
    _STANDALONE_PLUTO_PORT[] = Int(pluto_port)
    _STANDALONE_MCP_PORT[] = Int(mcp_port)
    _STANDALONE_REQUIRE_SECRET[] = require_secret_for_access
end

function standalone_session()
    _STANDALONE_SESSION[]
end

function pluto_running_standalone()
    _STANDALONE_SESSION[] !== nothing
end

function _notebook_summaries(session)
    [
        Dict{String,Any}(
            "notebook_id" => string(nb.notebook_id),
            "path"        => nb.path,
            "cell_count"  => length(nb.cell_order),
        )
        for nb in values(session.notebooks)
    ]
end

function session_status_dict()
    sess = _STANDALONE_SESSION[]
    binding = session_binding()
    pluto_port = _STANDALONE_PLUTO_PORT[]
    mcp_port = _STANDALONE_MCP_PORT[]
    pluto = sess === nothing ? "stopped" : "running"
    status = Dict{String,Any}(
        "pluto"      => pluto,
        "pluto_port" => pluto_port,
        "mcp_port"   => mcp_port,
        "notebooks"  => sess === nothing ? [] : _notebook_summaries(sess),
        "managed"    => binding !== nothing,
    )
    if binding !== nothing
        status["session_id"] = binding.session_id
        status["mcp_url"] = mcp_port === nothing ? nothing : "http://127.0.0.1:$mcp_port"
        status["pluto_url"] =
            (pluto == "running" && pluto_port !== nothing) ?
            "http://127.0.0.1:$pluto_port" : nothing
    else
        status["session_id"] = nothing
        status["mcp_url"] = mcp_port === nothing ? nothing : "http://127.0.0.1:$mcp_port"
        status["pluto_url"] =
            (pluto == "running" && pluto_port !== nothing) ?
            "http://127.0.0.1:$pluto_port" : nothing
    end
    status
end

function _publish_binding_ports!(; pluto::Union{Nothing,String}=nothing)::Nothing
    binding = session_binding()
    binding === nothing && return nothing
    lock(binding.lock) do
        binding.mcp_port = _STANDALONE_MCP_PORT[]
        binding.pluto_port = _STANDALONE_PLUTO_PORT[]
        if pluto !== nothing
            binding.pluto = pluto
        else
            binding.pluto = _STANDALONE_SESSION[] === nothing ? "stopped" : "running"
        end
    end
    write_binding_state!()
    nothing
end

function _handle_pluto_event(event)::Nothing
    if event isa Pluto.ServerStartEvent
        _STANDALONE_PLUTO_PORT[] = Int(event.port)
        _publish_binding_ports!(; pluto="running")
    elseif event isa Pluto.ShutdownNotebookEvent
        release_notebook_lease!(event.notebook.notebook_id)
    end
    nothing
end

function _init_pluto_session!(; pluto_port, pluto_port_hint, launch_browser, require_secret_for_access, notebook)
    bound = is_bound_session()
    opts = if bound
        Pluto.Configuration.from_flat_kwargs(
            port                      = nothing,
            port_hint                 = pluto_port_hint,
            launch_browser            = launch_browser,
            require_secret_for_access = require_secret_for_access,
            on_event                  = _handle_pluto_event,
        )
    else
        Pluto.Configuration.from_flat_kwargs(
            port                      = pluto_port,
            launch_browser            = launch_browser,
            require_secret_for_access = require_secret_for_access,
            on_event                  = _handle_pluto_event,
        )
    end
    sess = Pluto.ServerSession(; options = opts)
    if notebook !== nothing
        Pluto.SessionActions.open(sess, String(notebook); run_async = true)
    end
    _STANDALONE_PLUTO_TASK[] = @async begin
        try
            server = Pluto.run!(sess)
            _STANDALONE_PLUTO_SERVER[] = server
            wait(server)
        catch e
            isa(e, InterruptException) || @error "Pluto server error" exception=(e, catch_backtrace())
        finally
            _STANDALONE_PLUTO_SERVER[] = nothing
        end
    end
    deadline = time() + 30.0
    while time() < deadline
        _STANDALONE_PLUTO_SERVER[] !== nothing && break
        sleep(0.05)
    end
    _STANDALONE_PLUTO_SERVER[] === nothing &&
        error("Pluto failed to start within 30s (port=$pluto_port hint=$pluto_port_hint)")
    if bound
        deadline2 = time() + 5.0
        while time() < deadline2 && _STANDALONE_PLUTO_PORT[] === nothing
            sleep(0.05)
        end
        _STANDALONE_PLUTO_PORT[] === nothing &&
            error("Pluto started but ServerStartEvent.port was not observed (hint=$pluto_port_hint)")
    end
    sess
end

"""
    start_control_bridge!(; mcp_port_hint, listenany)

Bind the loopback MCP control HTTP server. In bound mode this runs at `connect()`
time so `/health` publishes the session nonce before Pluto starts.
"""
function start_control_bridge!(;
    mcp_port_hint::Int = _STANDALONE_MCP_PORT_HINT[],
    listenany::Bool = is_bound_session(),
)
    _STANDALONE_HTTP_SERVER[] !== nothing && return _STANDALONE_MCP_PORT[]

    ready = Channel{Any}(1)
    _STANDALONE_HTTP_TASK[] = @async begin
        try
            http_server = _HTTP_BRIDGE_RUNNER[](
                nothing,
                mcp_port_hint;
                listenany = listenany,
            )
            _STANDALONE_HTTP_SERVER[] = http_server
            actual = try
                HTTP.port(http_server)
            catch
                mcp_port_hint
            end
            _STANDALONE_MCP_PORT[] = Int(actual)
            _CONTROL_BRIDGE_OWNED[] = true
            put!(ready, Int(actual))
            wait(http_server)
        catch e
            put!(ready, e)
            isa(e, InterruptException) || rethrow()
        finally
            _STANDALONE_HTTP_SERVER[] = nothing
            _CONTROL_BRIDGE_OWNED[] = false
        end
    end

    result = take!(ready)
    result isa Exception && throw(result)
    _publish_binding_ports!()
    return result
end

"""
    start_pluto_stack!(; pluto_port, mcp_port, require_secret_for_access, launch_browser, notebook, http_async)

Start Pluto.run! and, when no control bridge is owned yet, the MCP HTTP bridge.
Idempotent when Pluto is already running.
"""
function start_pluto_stack!(;
    pluto_port::Union{Int,Nothing} = nothing,
    mcp_port::Union{Int,Nothing} = nothing,
    require_secret_for_access::Bool = _STANDALONE_REQUIRE_SECRET[],
    launch_browser::Bool = false,
    notebook = nothing,
    http_async::Bool = true,
)
    if _STANDALONE_SESSION[] !== nothing
        return session_status_dict()
    end

    bound = is_bound_session()
    if bound
        pluto_port !== nothing && throw(ArgumentError(
            "managed_ports::This Styx session allocates ports automatically; use pluto_session_status.pluto_url.",
        ))
        mcp_port !== nothing && throw(ArgumentError(
            "managed_ports::This Styx session allocates ports automatically; use pluto_session_status.pluto_url.",
        ))
    end

    resolved_pluto = pluto_port === nothing ? _STANDALONE_PLUTO_PORT_HINT[] : pluto_port
    resolved_mcp = mcp_port === nothing ? _STANDALONE_MCP_PORT_HINT[] : mcp_port
    if !bound
        _STANDALONE_PLUTO_PORT[] = resolved_pluto
        _STANDALONE_MCP_PORT[] = resolved_mcp
    else
        # Actual UI port comes from ServerStartEvent; clear until then.
        _STANDALONE_PLUTO_PORT[] = nothing
    end
    _STANDALONE_REQUIRE_SECRET[] = require_secret_for_access

    sess = _init_pluto_session!(;
        pluto_port = resolved_pluto,
        pluto_port_hint = _STANDALONE_PLUTO_PORT_HINT[],
        launch_browser,
        require_secret_for_access,
        notebook,
    )
    _STANDALONE_SESSION[] = sess
    _publish_binding_ports!(; pluto="running")

    # Legacy path: start HTTP with Pluto when connect() did not already bind a control bridge.
    if http_async && !_CONTROL_BRIDGE_OWNED[] && _STANDALONE_HTTP_SERVER[] === nothing
        _STANDALONE_HTTP_TASK[] = @async begin
            try
                http_server = _HTTP_BRIDGE_RUNNER[](sess, resolved_mcp; listenany=false)
                _STANDALONE_HTTP_SERVER[] = http_server
                wait(http_server)
            catch e
                isa(e, InterruptException) || rethrow()
            finally
                _STANDALONE_HTTP_SERVER[] = nothing
            end
        end
    end

    return session_status_dict()
end

function _close_standalone_http!()
    http_server = _STANDALONE_HTTP_SERVER[]
    if http_server !== nothing
        try
            close(http_server)
        catch
        end
        _STANDALONE_HTTP_SERVER[] = nothing
    end
    t = _STANDALONE_HTTP_TASK[]
    if t !== nothing && t !== current_task()
        try
            schedule(t, InterruptException(); error=true)
        catch
        end
        try
            wait(t)
        catch
        end
    end
    _STANDALONE_HTTP_TASK[] = nothing
    _CONTROL_BRIDGE_OWNED[] = false
end

function _close_standalone_pluto!()
    pluto_server = _STANDALONE_PLUTO_SERVER[]
    if pluto_server !== nothing
        try
            close(pluto_server)
        catch
        end
        _STANDALONE_PLUTO_SERVER[] = nothing
    end
    t = _STANDALONE_PLUTO_TASK[]
    if t !== nothing && t !== current_task()
        try
            wait(t)
        catch
        end
    end
    _STANDALONE_PLUTO_TASK[] = nothing
end

"""
    stop_pluto_stack!(; close_control_bridge=true)

Shut down Pluto notebooks and the Pluto server. When `close_control_bridge` is
false (bound `stop_pluto_session`), the control HTTP bridge stays up.
"""
function stop_pluto_stack!(; close_control_bridge::Bool = !is_bound_session())
    sess = _STANDALONE_SESSION[]
    if sess !== nothing
        for nb in collect(values(sess.notebooks))
            try
                Pluto.SessionActions.shutdown(sess, nb; async = false, verbose = false)
            catch
            end
        end
    end
    _STANDALONE_SESSION[] = nothing
    release_all_notebook_leases!()
    _close_standalone_pluto!()
    if close_control_bridge
        _close_standalone_http!()
    elseif is_bound_session()
        _STANDALONE_PLUTO_PORT[] = nothing
        _publish_binding_ports!(; pluto="stopped")
    end
    reset_staging_state!()
    return session_status_dict()
end

function require_standalone_session!()
    sess = _STANDALONE_SESSION[]
    sess === nothing &&
        throw(ArgumentError("pluto_not_running::Call start_pluto_session first."))
    return sess
end

function _lifecycle_get_notebook!(session, notebook_id_str)
    nid = try
        UUID(notebook_id_str)
    catch
        throw(ArgumentError("invalid_notebook_id::Invalid notebook ID: '$notebook_id_str'"))
    end
    nb = get(session.notebooks, nid, nothing)
    nb === nothing &&
        throw(KeyError("notebook_not_found::No notebook with id '$notebook_id_str' in the current session"))
    return nb
end

function _lifecycle_notify_browser(session, notebook)
    try
        Pluto.send_notebook_changes!(Pluto.ClientRequest(; session, notebook))
    catch
    end
end

"""
    allow_notebook_execution!(session, notebook; run_async=true, run_cells=true)

Programmatic equivalent of Glass **Run notebook code** for safe-preview notebooks
(local paths only). Mirrors Pluto `restart_process` when `run_cells=true`.

When `run_cells=false`, exits safe preview without queuing a full notebook run
(workspace starts lazily on the next `submit_changes` / `update_save_run!`).
"""
function allow_notebook_execution!(session, notebook; run_async::Bool=true, run_cells::Bool=true)
    ps = notebook.process_status
    if ps === Pluto.ProcessStatus.ready
        return Dict{String,Any}(
            "notebook_id"       => string(notebook.notebook_id),
            "execution_allowed" => true,
            "already_allowed"   => true,
            "ran"               => false,
            "process_status"    => string(ps),
        )
    end
    if ps !== Pluto.ProcessStatus.waiting_for_permission
        throw(ArgumentError(
            "execution_not_gated::Notebook is not in safe preview (process_status=$ps)",
        ))
    end
    if haskey(notebook.metadata, "risky_file_source")
        throw(ArgumentError(
            "risky_source::Cannot allow execution for risky remote sources via MCP; use Glass UI",
        ))
    end

    notebook.process_status = Pluto.ProcessStatus.waiting_to_restart
    session.options.evaluation.run_notebook_on_load &&
        Pluto._report_business_cells_planned!(notebook)
    _lifecycle_notify_browser(session, notebook)

    Pluto.SessionActions.shutdown(session, notebook; keep_in_session=true, async=true, verbose=false)

    if run_cells
        notebook.process_status = Pluto.ProcessStatus.starting
        _lifecycle_notify_browser(session, notebook)
        # Run through _run_cells! so edits staged during safe preview (pending_run)
        # clear once their cells complete. Non-blocking by default: sync_nbpkg +
        # reactive run stay off the MCP thread.
        _run_cells!(session, notebook, collect(notebook.cells); wait_for_completion=!run_async)
        _lifecycle_notify_browser(session, notebook)
        ran = true
    else
        # Exit the gate without a full run; next mutation starts the workspace.
        notebook.process_status = Pluto.ProcessStatus.ready
        _lifecycle_notify_browser(session, notebook)
        ran = false
    end

    Dict{String,Any}(
        "notebook_id"       => string(notebook.notebook_id),
        "execution_allowed" => true,
        "already_allowed"   => false,
        "ran"               => ran,
        "process_status"    => string(notebook.process_status),
    )
end

# ---------------------------------------------------------------------------
# Lifecycle tool implementations
# ---------------------------------------------------------------------------

function tool_pluto_session_status(_args)
    session_status_dict()
end

function tool_start_pluto_session(args)
    bound = is_bound_session()
    if bound
        if haskey(args, "pluto_port") && args["pluto_port"] !== nothing
            throw(ArgumentError(
                "managed_ports::This Styx session allocates ports automatically; use pluto_session_status.pluto_url.",
            ))
        end
        if haskey(args, "mcp_port") && args["mcp_port"] !== nothing
            throw(ArgumentError(
                "managed_ports::This Styx session allocates ports automatically; use pluto_session_status.pluto_url.",
            ))
        end
        return start_pluto_stack!()
    end
    pluto_port = get(args, "pluto_port", _STANDALONE_PLUTO_PORT_HINT[])
    mcp_port = get(args, "mcp_port", _STANDALONE_MCP_PORT_HINT[])
    start_pluto_stack!(; pluto_port = Int(pluto_port), mcp_port = Int(mcp_port))
end

function tool_stop_pluto_session(_args)
    # Bound mode: keep control bridge; legacy: tear everything down.
    stop_pluto_stack!(; close_control_bridge = !is_bound_session())
end

function tool_open_notebook(args)
    sess = require_standalone_session!()
    path = get(args, "path", nothing)
    path === nothing && throw(ArgumentError("invalid_path::path is required"))
    path = String(path)
    ispath(path) || throw(ArgumentError("file_not_found::No file at '$path'"))
    run_nb = get(args, "run_notebook", false)

    # SessionActions.open already queues update_save_run! when execution_allowed;
    # do not call tool_run_all_cells again (double-run starved MCP / raced executetoken).
    nb = Pluto.SessionActions.open(sess, path; run_async = true, execution_allowed = run_nb)

    result = Dict{String,Any}(
        "notebook_id"         => string(nb.notebook_id),
        "path"                => nb.path,
        "execution_allowed"   => run_nb,
        "ran"                 => run_nb,
        "process_status"      => string(nb.process_status),
    )
    if run_nb
        result["warnings"] = String[
            "async_execution::open queued non-blocking notebook run; poll read_cell for completion",
        ]
    end
    return result
end

function tool_new_notebook(args)
    require_standalone_session!()
    requested = get(args, "path", nothing)
    nb = if requested === nothing
        # Pluto's own naming in its new-notebooks directory, like "Create a new notebook".
        Pluto.emptynotebook()
    else
        path = abspath(expanduser(String(requested)))
        endswith(path, ".jl") ||
            throw(ArgumentError("invalid_path::Notebook path must end in .jl: '$path'"))
        ispath(path) &&
            throw(ArgumentError("file_exists::'$path' already exists; use open_notebook to load it"))
        isdir(dirname(path)) ||
            throw(ArgumentError("invalid_path::Directory does not exist: '$(dirname(path))'"))
        Pluto.emptynotebook(path)
    end
    # Pluto serializes the file (never a hand-written header), then the normal open
    # path loads it: same safe preview and bound-mode path leases as open_notebook.
    Pluto.save_notebook(nb, nb.path)
    result = tool_open_notebook(Dict{String,Any}("path" => nb.path))
    result["created"] = true
    return result
end

function tool_allow_execution(args)
    sess = require_standalone_session!()
    notebook_id = get(args, "notebook_id", nothing)
    notebook_id === nothing &&
        throw(ArgumentError("invalid_notebook_id::notebook_id is required"))
    nb = _lifecycle_get_notebook!(sess, String(notebook_id))
    run_cells = get(args, "run_notebook", true)
    # Single path: allow_notebook_execution! already queues the run when requested.
    # A follow-up tool_run_all_cells was a double-run footgun on the stdio thread.
    result = allow_notebook_execution!(sess, nb; run_async=true, run_cells=run_cells)
    if get(result, "ran", false)
        result["run_warnings"] = String[
            "async_execution::allow_execution queued non-blocking notebook run; poll read_cell for completion",
        ]
    end
    result["process_status"] = string(nb.process_status)
    return result
end

function call_lifecycle_tool(name::AbstractString, arguments)
    if name == "pluto_session_status"
        tool_pluto_session_status(arguments)
    elseif name == "start_pluto_session"
        tool_start_pluto_session(arguments)
    elseif name == "stop_pluto_session"
        tool_stop_pluto_session(arguments)
    elseif name == "open_notebook"
        tool_open_notebook(arguments)
    elseif name == "new_notebook"
        tool_new_notebook(arguments)
    elseif name == "allow_execution"
        tool_allow_execution(arguments)
    else
        throw(ArgumentError("unknown_tool::Unknown lifecycle tool: '$name'"))
    end
end

function call_tool_with_session(session, name::AbstractString, arguments)
    if is_lifecycle_tool(name)
        return call_lifecycle_tool(name, arguments)
    end
    sess = session === nothing ? standalone_session() : session
    sess === nothing &&
        throw(ArgumentError("pluto_not_running::Call start_pluto_session first."))
    return call_tool(sess, name, arguments)
end

"""Test helper: bind an in-memory session as the standalone Pluto session."""
function bind_standalone_session!(sess)
    _STANDALONE_SESSION[] = sess
end
